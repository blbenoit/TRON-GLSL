// Author: Patricio Gonzalez Vivo
// Title: Hillshading

#ifdef GL_ES
precision mediump float;
#endif

#define PI 3.1415926535897932384626433832795

uniform sampler2D u_tex0; // https://s3.amazonaws.com/elevation-tiles-prod/normal/11/386/803.png
uniform vec2 u_tex0Resolution;

uniform vec2 u_resolution;
uniform vec2 u_mouse;
uniform float u_time;

float stripes(vec2 st, float size, float width){
    st *= size;
    return step(width,abs(sin((st.y-st.x)*PI)));
}

vec3 palette( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d ) {
    return a + b*cos( 6.28318*(c*t+d) );
}

void main(){
    vec2 st = gl_FragCoord.xy/u_resolution.xy;
    if (u_resolution.y < u_resolution.x ) {
        st.y *= u_resolution.y/u_resolution.x;
        st.y -= (u_resolution.y*.5-u_resolution.x*.5)/u_resolution.x;
    } else {
        st.x *= u_resolution.x/u_resolution.y;
        st.x -= (u_resolution.x*.5-u_resolution.y*.5)/u_resolution.y;
    }
    
    float t = -u_time*0.2;
    vec4 tile = texture2D(u_tex0,st);
    vec3 normal = tile.xyz*2.-1.;
    float depth = tile.a;
    
    float pct = abs(dot(normal,normalize(vec3(u_mouse/u_resolution-.5,.25))));
    vec3 color = vec3(0.);
    color = palette(depth+t*0.1,vec3(.5),vec3(.5),vec3(2.,1.,0.),vec3(.5,.2,0.25));
    color = mix(color,
                vec3(0.000),
                stripes(st,50.,pct));

    gl_FragColor = vec4(color,1.0);
}
